plugins {
	id 'org.springframework.boot' version '2.7.4'
	id 'io.spring.dependency-management' version '1.0.14.RELEASE'
	id 'java'
	id "de.undercouch.download" version "5.2.1"
}

group = 'com.charlesbot'
version = '1.0.0-SNAPSHOT'
sourceCompatibility = '17'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
	maven { url 'https://jitpack.io' }
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	implementation('org.springframework.boot:spring-boot-starter-web') {
		exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
	}
	implementation 'org.springframework.boot:spring-boot-starter-jetty'
	implementation 'net.sf.supercsv:super-csv:2.4.0'
	implementation 'com.google.guava:guava:31.1-jre'
	implementation 'com.brsanthu:data-exporter:1.0.4'
	implementation 'commons-cli:commons-cli:1.5.0'
	implementation 'com.github.Ullink:simple-slack-api:1.4.0'
	implementation 'redis.clients:jedis'
	compileOnly 'org.projectlombok:lombok:1.18.24'
	runtimeOnly 'com.newrelic.agent.java:newrelic-agent:6.2.1'
	annotationProcessor 'org.projectlombok:lombok:1.18.24'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
	useJUnitPlatform()
}

task copyDependencies(type: Copy) {
	from configurations.runtimeClasspath
	into "$buildDir/newrelic"
	include "**/newrelic*.jar"
	configurations.runtimeClasspath.allDependencies.each {
		rename "-${it.version}", ""
	}
}

task stage(dependsOn: ['build', 'clean', 'copyDependencies'])
build.mustRunAfter clean

